plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

task runCheckstyleJavaDocs(type: GradleBuild) {
    tasks = ["javadoc", "checkstyleMain", "checkstyleTest"]
}
task cleanTest(type: GradleBuild) {
    tasks = ["clean", "test"]
}

application {
    mainClassName = "taskbot.main.Main"
}

shadowJar {
    archiveBaseName = "TaskBot"
    archiveClassifier = null
}

checkstyle {
    toolVersion = '8.32'
}

run {
    standardInput = System.in
    enableAssertions = true
}
