plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0'

    String javaFxVersion = '11'

    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'linux'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClassName = "duke.Main"
}

shadowJar {
    archiveBaseName = "duke"
    archiveClassifier = null
}

checkstyle {
    toolVersion = '8.29'
}

run{
    standardInput = System.in
}

/**
 * @@author akgrenSoar-reused
 * Solution adapted from: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.Exec.html
 */
task runtest(type: Exec) {
    dependsOn 'shadowJar'
    workingDir "./text-ui-test"
    commandLine 'cmd', '/C', 'runtest.bat'
}

task cmdCheck {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'test' // JUnit test
    dependsOn 'runtest' // text-ui-test
}

task guiCheck {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'test' // JUnit test
}

/**
 * @@author akgrenSoar-reused
 * Solution adapted from: https://stackoverflow.com/a/45607934/6943913
 */
task myBuild(type: GradleBuild) {
    tasks = ['clean',
             'javadoc',
             'shadowJar'
    ]
}

